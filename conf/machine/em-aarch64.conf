# SPDX-License-Identifier: MIT
#
# Copyright (C) 2024 TQ-Systems GmbH <oss@ew.tq-group.com>, D-82229 Seefeld, Germany.
# Author: Matthias Schiffer

#@TYPE: Machine
#@NAME: TQ-Systems EM (AArch64)
#@DESCRIPTION: Combined machine configuration for AArch64-based TQ-Systems Energy Manager

MACHINEOVERRIDES =. "em:"

require conf/machine/include/arm/arch-arm64.inc

PREFERRED_PROVIDER_virtual/kernel = "linux-em"
PREFERRED_PROVIDER_virtual/bootloader = "em-aarch64-bootloader"

KERNEL_DEVICETREE = " \
    freescale/imx8mn-em4xx-l-ksz8863.dtb \
    freescale/imx8mn-em4xx-l-mv88e6020.dtb \
    freescale/imx8mn-em4xx-u.dtb \
    ti/k3-am625-em-cb30.dtb \
"

KERNEL_IMAGETYPE = "Image.gz"

# Console information is taken from cmdline/DTB. SERIAL_CONSOLES is set to " "
# to make it non-empty, resulting in serial-getty@.service to be installed,
# thus allowing systemd's console auto detection to work.
SERIAL_CONSOLES = " "
USE_VT = "0"

MACHINE_FEATURES = "ext2 vfat"

TQ_DEVICE_TYPE = "hw02xx"
TQ_DEVICE_SUBTYPE = '{"tq,em4xx": "hw0200", "tq,am625-em-cb30": "hw0210"}'
TQ_PRODUCT_ID = '{"tq,em4xx": 18530, "tq,am625-em-cb30": 18546}'

EM_BUNDLE_BOOTLOADER = " \
    tq,em4xx:512m \
    tq,em4xx:1024m \
    tq,am625-em-cb30:512m \
    tq,am625-em-cb30:1024m \
    tq,am625-em-cb30:2048m \
"
EM_BUNDLE_BOOTLOADER[tq_em4xx_512m] = "bootloader-em4xx-512m.bin"
EM_BUNDLE_BOOTLOADER[tq_em4xx_1024m] = "bootloader-em4xx-1g.bin"
EM_BUNDLE_BOOTLOADER[tq_am625-em-cb30_512m] = "bootloader-em-cb30-512m.bin"
EM_BUNDLE_BOOTLOADER[tq_am625-em-cb30_1024m] = "bootloader-em-cb30-1g.bin"
EM_BUNDLE_BOOTLOADER[tq_am625-em-cb30_2048m] = "bootloader-em-cb30-2g.bin"

MACHINE_EXTRA_RRECOMMENDS += " \
    eol-led \
    status-led \
    netdev-led \
    em-power-handler \
    teridiand-config \
    em-timesync \
    linux-firmware-imx-sdma-imx7d \
    em-aarch64-bootloader-env \
"

BBMULTICONFIG += "em4xx em-cb30 em-cb30-k3r5"
MULTICONFIG_VARS += "DEPLOY_DIR_IMAGE LICENSE_DIRECTORY PREFERRED_PROVIDER_virtual/bootloader"
INHERIT += "multiconfig-vars"
